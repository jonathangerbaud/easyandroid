// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: "./dependencies.gradle"
    apply from: "./androidconfig.gradle"
    addRepos(repositories)

    dependencies {
        classpath deps.android_gradle_plugin
        classpath deps.android_gradle_kotlin
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
    repositories {
        mavenCentral()
    }
}

allprojects {
    addRepos(repositories)
}

subprojects {
    // it = module (app, core, gms_location, ...
    if (it.name != 'app') {
        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android-extensions'
        apply plugin: 'kotlin-android'

        // Trick to get several libraries in one Github project
        // and then only get the libraries we need via JitPack
        apply plugin: 'com.github.dcendents.android-maven'
        group='com.github.jonathangerbaud.easyandroid'

        android {
            compileSdkVersion androidconfig.compileSdkVersion

            defaultConfig {
                minSdkVersion androidconfig.minSdkVersion
                targetSdkVersion androidconfig.targetSdkVersion
                versionCode androidconfig.versionCode
                versionName androidconfig.versionName
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            compileOptions {
                sourceCompatibility = '1.8'
                targetCompatibility = '1.8'
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
